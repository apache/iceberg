/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

// The purpose of this module is to re-shade org.apache.orc.storage to the original org.apache.hadoop.hive package
// name. This is to be used by Hive3 for features including e.g. vectorization.
project(':iceberg-hive3-orc-bundle') {

  apply plugin: 'com.github.johnrengelman.shadow'

  tasks.jar.dependsOn tasks.shadowJar

  configurations {
    implementation {
      exclude group: 'com.github.luben'
    }
  }

  dependencies {
    implementation project(':iceberg-data')
    implementation project(':iceberg-orc')

    testCompileOnly project(path: ':iceberg-data', configuration: 'testArtifacts')
    testCompileOnly project(path: ':iceberg-orc', configuration: 'testArtifacts')
  }

  shadowJar {
    configurations = [project.configurations.compileClasspath, project.configurations.runtimeClasspath]

    zip64 true

    // include the LICENSE and NOTICE files for the shaded Jar
    from(projectDir) {
      include 'LICENSE'
      include 'NOTICE'
    }

    // Relocate dependencies to avoid conflicts
    relocate 'org.apache.orc.storage', 'org.apache.hadoop.hive'

    // We really only need Iceberg and Orc classes, but with relocated references for storage-api classes (see above)
    // Unfortunately the include list feature of this shader plugin doesn't work as expected
    dependencies {
      exclude 'com/**/*'
      exclude 'edu/**/*'
      exclude 'io/**'
      exclude 'javax/**'
      exclude 'org/apache/avro/**/*'
      exclude 'org/apache/commons/**/*'
      exclude 'org/checkerframework/**/*'
      exclude 'org/codehaus/**/*'
      exclude 'org/intellij/**/*'
      exclude 'org/jetbrains/**/*'
      exclude 'org/slf4j/**/*'
      exclude 'org/threeten/**/*'
      exclude 'org/xerial/**/*'
      exclude 'org/apache/parquet/**/*'
      exclude 'org/apache/yetus/**/*'
      exclude 'shaded/parquet/**/*'
    }

    classifier null
  }

  jar {
    enabled = false
  }

}

project(':iceberg-hive3') {

  // run the tests in iceberg-mr with Hive3 dependencies
  sourceSets {
    test {
      java.srcDirs = ['../mr/src/test/java', 'src/test/java']
      resources.srcDirs = ['../mr/src/test/resources', 'src/test/resources']
    }
  }

  // exclude these Hive2-specific tests from iceberg-mr
  test {
    exclude '**/TestIcebergDateObjectInspector.class'
    exclude '**/TestIcebergTimestampObjectInspector.class'
    exclude '**/TestIcebergTimestampWithZoneObjectInspector.class'
  }

  dependencies {
    compileOnly project(path: ':iceberg-bundled-guava', configuration: 'shadow')
    compileOnly project(':iceberg-api')
    compileOnly project(':iceberg-core')
    compileOnly project(':iceberg-common')
    compileOnly project(':iceberg-hive-metastore')
    compileOnly project(':iceberg-parquet')
    compileOnly project(':iceberg-hive3-orc-bundle')
    compileOnly project(':iceberg-mr')
    compileOnly project(':iceberg-data')

    compileOnly("org.apache.hadoop:hadoop-client:3.1.0") {
      exclude group: 'org.apache.avro', module: 'avro'
    }

    compileOnly("org.apache.hive:hive-exec:3.1.2:core") {
      exclude group: 'com.google.code.findbugs', module: 'jsr305'
      exclude group: 'com.google.guava'
      exclude group: 'com.google.protobuf', module: 'protobuf-java'
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.apache.calcite.avatica'
      exclude group: 'org.apache.hive', module: 'hive-llap-tez'
      exclude group: 'org.apache.logging.log4j'
      exclude group: 'org.pentaho' // missing dependency
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }

    compileOnly("org.apache.orc:orc-core::nohive") {
      exclude group: 'org.apache.hadoop'
      exclude group: 'commons-lang'
      // These artifacts are shaded and included in the orc-core fat jar
      exclude group: 'com.google.protobuf', module: 'protobuf-java'
      exclude group: 'org.apache.hive', module: 'hive-storage-api'
    }

    compileOnly("org.apache.hive:hive-metastore:3.1.2") {
      exclude group: 'org.apache.orc'
    }
    compileOnly("org.apache.hive:hive-serde:3.1.2") {
      exclude group: 'org.apache.orc'
    }

    testImplementation project(path: ':iceberg-api', configuration: 'testArtifacts')
    testImplementation project(path: ':iceberg-core', configuration: 'testArtifacts')
    testImplementation project(path: ':iceberg-hive-metastore', configuration: 'testArtifacts')
    testImplementation project(path: ':iceberg-data', configuration: 'testArtifacts')

    testImplementation("org.apache.avro:avro:1.9.2")
    testImplementation("org.apache.calcite:calcite-core")
    testImplementation("com.esotericsoftware:kryo-shaded:4.0.2")
    testImplementation("com.fasterxml.jackson.core:jackson-annotations:2.6.5")
    testImplementation("org.apache.hive:hive-service:3.1.2") {
      exclude group: 'org.apache.hive', module: 'hive-exec'
      exclude group: 'org.apache.orc'
    }
    testImplementation("org.apache.tez:tez-dag:0.10.1")
    testImplementation("org.apache.tez:tez-mapreduce:0.10.1")
  }

  test {
    // testJoinTables / testScanTable
    maxHeapSize '2500m'
  }
}
