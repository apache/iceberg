/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

project(':iceberg-mr') {
  configurations {
    testImplementation {
      exclude group: 'org.apache.parquet', module: 'parquet-hadoop-bundle'
    }
  }
  test {
    useJUnitPlatform()
  }

  dependencies {
    implementation project(path: ':iceberg-bundled-guava', configuration: 'shadow')
    api project(':iceberg-api')
    implementation project(':iceberg-common')
    implementation project(':iceberg-core')
    api project(':iceberg-data')
    implementation project(':iceberg-hive-metastore')
    implementation project(':iceberg-orc')
    implementation project(':iceberg-parquet')

    compileOnly(libs.hadoop2.client) {
      exclude group: 'org.apache.avro', module: 'avro'
    }

    compileOnly("${libs.hive2.exec.get().module}:${libs.hive2.exec.get().getVersion()}:core") {
      exclude group: 'com.google.code.findbugs', module: 'jsr305'
      exclude group: 'com.google.guava'
      exclude group: 'com.google.protobuf', module: 'protobuf-java'
      exclude group: 'org.apache.avro', module: 'avro'
      exclude group: 'org.apache.calcite.avatica'
      exclude group: 'org.apache.hive', module: 'hive-llap-tez'
      exclude group: 'org.apache.logging.log4j'
      exclude group: 'org.pentaho' // missing dependency
      exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    }
    compileOnly libs.hive2.metastore
    compileOnly libs.hive2.serde

    implementation libs.caffeine

    testImplementation libs.calcite.core
    testImplementation libs.calcite.druid

    testImplementation project(path: ':iceberg-data', configuration: 'testArtifacts')
    testImplementation project(path: ':iceberg-api', configuration: 'testArtifacts')
    testImplementation project(path: ':iceberg-core', configuration: 'testArtifacts')
    testImplementation project(path: ':iceberg-hive-metastore', configuration: 'testArtifacts')

    testImplementation libs.avro.avro
    testImplementation libs.calcite.core
    testImplementation libs.kryo.shaded
    testImplementation platform(libs.jackson.bom)
    testImplementation "com.fasterxml.jackson.core:jackson-annotations"
    testImplementation(libs.hive2.service) {
      exclude group: 'org.apache.hive', module: 'hive-exec'
    }
    testImplementation libs.tez09.dag
    testImplementation libs.tez09.mapreduce
  }

  test {
    // testJoinTables / testScanTable
    maxHeapSize '2500m'
  }
}

