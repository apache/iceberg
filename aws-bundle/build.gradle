/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

project(":iceberg-aws-bundle") {

  apply plugin: 'com.github.johnrengelman.shadow'

  tasks.jar.dependsOn tasks.shadowJar

  dependencies {
    implementation platform(libs.awssdk.bom)
    implementation "software.amazon.awssdk:apache-client"
    implementation "software.amazon.awssdk:auth"
    implementation "software.amazon.awssdk:iam"
    implementation "software.amazon.awssdk:sso"
    implementation "software.amazon.awssdk:s3"
    implementation "software.amazon.awssdk:kms"
    implementation "software.amazon.awssdk:glue"
    implementation "software.amazon.awssdk:sts"
    implementation "software.amazon.awssdk:dynamodb"
    implementation "software.amazon.awssdk:lakeformation"
    implementation "software.amazon.awssdk:url-connection-client"
  }

  shadowJar {
    archiveClassifier.set(null)
    zip64 true

    // include the LICENSE and NOTICE files for the shaded Jar
    from(projectDir) {
      include 'LICENSE'
      include 'NOTICE'
    }

    // relocate AWS-specific versions
    relocate 'org.apache.http', 'org.apache.iceberg.aws.shaded.org.apache.http'
    relocate 'io.netty', 'org.apache.iceberg.aws.shaded.io.netty'
  }

  jar {
    enabled = false
  }
}
