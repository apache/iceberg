/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

project(':iceberg-aliyun-runtime') {
  apply plugin: 'com.github.johnrengelman.shadow'

  tasks.jar.dependsOn tasks.shadowJar

  configurations {
    implementation {
      exclude group: 'org.slf4j'
    }
  }

  dependencies {
    implementation project(':iceberg-aliyun')
  }

  shadowJar {
    configurations = [project.configurations.runtimeClasspath]

    zip64 true

    // include the LICENSE and NOTICE files for the shaded Jar
    from(projectDir) {
      include 'LICENSE'
      include 'NOTICE'
    }

    // Relocate dependencies to avoid conflicts
    relocate 'com.google', 'org.apache.iceberg.shaded.com.google'
    relocate 'com.fasterxml', 'org.apache.iceberg.shaded.com.fasterxml'
    relocate 'com.github.benmanes', 'org.apache.iceberg.shaded.com.github.benmanes'
    relocate 'org.checkerframework', 'org.apache.iceberg.shaded.org.checkerframework'
    relocate 'org.apache.avro', 'org.apache.iceberg.shaded.org.apache.avro'
    // Relocate to org.apache.iceberg.aliyun.shaded.* because they are introduced by iceberg-aliyun modules.
    relocate 'org.apache.http', 'org.apache.iceberg.aliyun.shaded.org.apache.http'
    relocate 'org.jdom2', 'org.apache.iceberg.aliyun.shaded.org.jdom2'
    relocate 'org.codehaus.jettison', 'org.apache.iceberg.aliyun.shaded.org.codehaus.jettison'
    relocate 'org.apache.commons', 'org.apache.iceberg.aliyun.shaded.org.apache.commons'
    relocate 'org.apache.http', 'org.apache.iceberg.aliyun.shaded.org.apache.http'
    relocate 'javax.xml', 'org.apache.iceberg.aliyun.shaded.javax.xml'

    classifier null
  }

  jar {
    enabled = false
  }
}
