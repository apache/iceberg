/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

project(":iceberg-gcp-bundle") {

  apply plugin: 'com.gradleup.shadow'

  tasks.jar.dependsOn tasks.shadowJar

  dependencies {
    implementation platform(libs.google.libraries.bom)
    implementation "com.google.cloud:google-cloud-storage"
  }

  shadowJar {
    archiveClassifier.set(null)
    zip64 true

    // include the LICENSE and NOTICE files for the shaded Jar
    from(projectDir) {
      include 'LICENSE'
      include 'NOTICE'
    }

    dependencies {
      exclude(dependency('org.slf4j:slf4j-api'))
    }

    // relocate GCP-specific versions
    relocate 'com.fasterxml.jackson', 'org.apache.iceberg.gcp.shaded.com.fasterxml.jackson'
    relocate 'com.google.common', 'org.apache.iceberg.gcp.shaded.com.google.common'
    relocate 'com.google.errorprone', 'org.apache.iceberg.gcp.shaded.com.google.errorprone'
    relocate 'com.google.gson', 'org.apache.iceberg.gcp.shaded.com.google.gson'
    relocate 'com.google.protobuf', 'org.apache.iceberg.gcp.shaded.com.google.protobuf'
    relocate 'org.apache.http', 'org.apache.iceberg.gcp.shaded.org.apache.http'
    relocate 'io.netty', 'org.apache.iceberg.gcp.shaded.io.netty'
  }

  jar {
    enabled = false
  }
}
